*** Settings ***

Resource  plone.txt

Suite Setup  Setup

*** Variables ***

*** Test cases ***

Test Add Employee
    Goto Homepage
    Add Employee  Douglas  Adams

Test Edit Employee
    Goto Homepage
    Edit Employee  Douglas Adams  m  11  3  1952

Test Remove Employee
    Goto Homepage
    Remove Employee  Douglas Adams
    Page Should Contain  Plone site

*** Keywords ***

Setup
    Log In  admin  admin

Add Employee
    [arguments]  ${given_name}  ${surname}

    Open Add New Menu
    Click Link  css=a#s17-employee
    Page Should Contain  Add Employee
    Input Text  css=input#form-widgets-given_name  ${given_name}
    Input Text  css=input#form-widgets-surname  ${surname}
    Click Button  Save
    Page Should Contain  Item created

Edit Employee
    [arguments]  ${employee}  ${gender}  ${day}  ${month}  ${year}

    Click Link  ${employee}
    Click Link  Edit
    Page Should Contain  Edit Employee
    Select From List  css=select#form-widgets-gender  ${gender}
    Input Text  css=input#form-widgets-birthday-day  ${day}
    Select From List  css=select#form-widgets-birthday-month  ${month}
    Input Text  css=input#form-widgets-birthday-year  ${year}
    Click Button  Save
    Page Should Contain  Changes saved

Remove Employee
    [arguments]  ${employee}

    Click Link  ${employee}
    Click Delete Action
    Wait Until Page Contains  Do you really want to delete this item?
    Click Button  Delete

